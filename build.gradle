buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
		maven {
			name 'm2'
			url 'https://plugins.gradle.org/m2'
		}
		maven {
			name = 'sponge'
			url = 'http://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
		classpath 'gradle.plugin.mnm.mods.kappa:ap-ide:1.0.5'
	}
}

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'mnm.gradle.ap-ide'

group = 'net.shadowboxx.skindepot'
version = '1.3.1'
description = 'SkinDepot'

targetCompatibility = 1.8
sourceCompatibility = 1.8

minecraft {
	version = "1.12.2"
	mappings = 'snapshot_20171003'
	runDir = 'run'
	replace '@VERSION@',project.version
}

sourceSets {
	def deps = [
		configurations.forgeGradleMcDeps,
		configurations.forgeGradleMc,
		configurations.compile,
	]
	main {
		compileClasspath += files deps
		refMap = 'mixin.skindepot.refmap.json'
	}

}

litemod.json {
	author = 'Palejack (originally Verdana, Rene_Z, Mumfrey, JoyJoy)'
	description = 'SkinDepot is a fork of the VoxelMod Team HDSkins mod which allows players to set and see skins from the SkinDepot skin repository.'
	mixinConfigs += [
		'mixin.skindepot.json'
	]
}

afterEvaluate {
	dependencies {
		// add mixin to the factory path
		// use same version as liteloader
		def liteloader = project.plugins['net.minecraftforge.gradle.liteloader']
		def artifact = liteloader.artifact.libraries
			.collect {it.name}
			.find {it.contains 'mixin'}
		factory artifact
	}
	processor {
		// tell the processor where the reobf file is
		options.reobfSrgFile = mixin.reobfSrgFile
	}
}

// manifest entries for all jars
def jarManifest = {
    attributes (
        'Built-By': 'Palejack',
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Implementation-Title': 'SkinDepot',
        'Implementation-Version': version,
        'Implementation-Vendor': 'skindepot.shadowboxx.net'
    )
}

jar {
	from sourceSets.main.output
	from litemod.outputs
	manifest jarManifest
}

sourceJar {
	// add skindepot sources
	from sourceSets.main.allSource
	manifest jarManifest
}
task srgJar(type: Jar) {
	from sourceSets.main.output
	from litemod.outputs
	classifier "mc$minecraft.version-srg"
	baseName "mod-${project.name.toLowerCase()}"
}

reobf {
    jar {
        mappingType = 'NOTCH'
    }
	srgJar {
		mappingType = 'SEARGE'
	}
}
mixin {
	defaultObfuscationEnv notch
}

// windows only
task deploy(type: Copy, dependsOn: build) {
	from jar.archivePath
	into file("$System.env.APPDATA/.minecraft/mods")
}
